#!/usr/bin/env nu

use ~/dot/nu/picker.nu *

def main [$cmd = 'xdg-open'] {
    ^($cmd) (main pick)
}

# Open bookmark picker
def "main pick" [] {
    open-file | pick-value
}

# List bookmarks
def "main list" [] {
    open-file
}

# Add a URL, prompting the user for a key
def "main add-url" [$url: string] {
    let $key = (input 'Key: ')
    main add $key $url
}

# Add a bookmark
def "main add" [$key: string, $url: string] {
    open-file | insert $key $url | save-to-file
}

# Set URL by key
def "main set" [$key: string, $url: string] {
    open-file | update $key $url | save-to-file
}

# Edit bookmarks.json with $EDITOR
def "main edit" [] {
    let $editor = $env.EDITOR? | default micro
    ^($editor) (file-path)
}

#-------------------------------------------------------------------------------
# PRIVATE

const $FILE_PATH = '~/me/bookmarks.json'

def file-path []: nothing -> string {
    $FILE_PATH | path expand
}

def open-file []: nothing -> record {
    open (file-path)
}

def save-to-file []: record -> nothing {
    $in | to json | save -f (file-path)
}
