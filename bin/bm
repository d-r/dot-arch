#!/usr/bin/env nu

use ~/dot/nu/kit.nu *
use ~/dot/nu/niri.nu *

const $FILE = '~/brain/data/bookmarks.toml' | path expand

# Bookmarks
def main [] {
    wm spawn xdg-open (main pick)
}

# Open bookmark picker
def "main pick" [] {
    (main list | pick --match-column key).url
}

# List bookmarks
def "main list" [] {
    open-file | transpose key url
}

# Save a URL, prompting the user for a key
def "main save" [$url: string] {
    let $key = (gum input --placeholder $"Name for ($url)")
    let $bs = (open-file)
    if $key in $bs {
        let $url = $bs | get $key
        print $'"($key)" is already assigned to ($url)'
        gum confirm "Overwrite?"
        # If the user chose "No", the command terminates here
    }
    $bs | insert $key $url | save-to-file
    notify-send "Bookmark saved"
}

# Edit bookmarks with $EDITOR
def "main edit" [] {
    let $editor = $env.EDITOR? | default micro
    ^($editor) $FILE
}

def open-file []: nothing -> record {
    open $FILE
}

def save-to-file []: record -> nothing {
    $in | to toml | save -f $FILE
}
