#!/usr/bin/env nu

use ~/dot/nu/kit.nu *
use ~/dot/nu/niri.nu *

def main [] {
    wm spawn xdg-open (main pick)
}

# Open bookmark picker
def "main pick" [] {
    (main list | pick --match-column key).url
}

# List bookmarks
def "main list" [] {
    open-file | transpose key url
}

# Add a URL, prompting the user for a key
def "main add-url" [$url: string] {
    let $key = (input 'Key: ')
    main add $key $url
}

# Add a bookmark
def "main add" [$key: string, $url: string] {
    open-file | insert $key $url | save-to-file
}

# Set URL by key
def "main set" [$key: string, $url: string] {
    open-file | update $key $url | save-to-file
}

# Edit bookmarks.json with $EDITOR
def "main edit" [] {
    let $editor = $env.EDITOR? | default micro
    ^($editor) (file-path)
}

#-------------------------------------------------------------------------------
# PRIVATE

const $FILE_PATH = '~/bucket/bookmarks.json'

def file-path []: nothing -> string {
    $FILE_PATH | path expand
}

def open-file []: nothing -> record {
    open (file-path)
}

def save-to-file []: record -> nothing {
    $in | to json | save -f (file-path)
}
