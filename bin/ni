#!/usr/bin/env nu

use ~/dot/nu/kit.nu *

# The knight who says "Ni!"
def main [] {
    help main
}

#-------------------------------------------------------------------------------
# HIGH-LEVEL

# Spawn a terminal
def "main toggle-term" [] {
    let $w = (main windows-in-focused-workspace | where app_id == term | first?)
    if ($w | is-empty) {
        kitty --app-id term
    } else if $w.is_focused {
        main focus-previous-window
    } else {
        main focus-window $w.id
    }
}

# Run a CLI app inside of a terminal
def "main spawn-cli-app" [$id: string, ...$cmd] {
    kitty --app-id $id ...$cmd
}

# Pick something from a menu
def "main pick" [] {
    help main pick
}

# Pick a bookmark
def "main pick bookmark" [] {
    modal bm
}

# Pick an application
def "main pick app" [] {
    modal app
}

# Pick a project
def "main pick project" [] {
    modal pro
}

# Run a command inside of a modal terminal
def --wrapped modal [...$cmd] {
    let $pid = (main windows | where app_id == modal | get pid.0?)
    if ($pid | is-empty) {
        kitty --config ~/.config/kitty/modal.conf --app-id modal ...$cmd
    } else {
        kill $pid
    }
}

#-------------------------------------------------------------------------------
# ACTIONS

alias act = niri msg action

# Spawn a command
def --wrapped "main spawn" [...$cmd] {
    act spawn -- ...$cmd
}

# Focus a window by ID
def "main focus-window" [$id] {
    act focus-window --id $id
}

# Focus the previously focused window
def "main focus-previous-window" [] {
    act focus-window-previous
}

# Focus a workspace by index/name
def "main focus-workspace" [$ref] {
    act focus-workspace $ref
}

# Focus the previously focused workspace
def "main focus-previous-workspace" [] {
    act focus-workspace-previous
}

#-------------------------------------------------------------------------------
# QUERIES

def --wrapped msg [...$args] {
    niri msg -j ...$args | from json
}

# Get the focused window
def "main focused-window" [] {
    main windows-in-focused-workspace | where is_focused == true | first?
}

# List windows in the focused workspace
def "main windows-in-focused-workspace" [] {
    main windows | where workspace_id == (main focused-workspace).id
}

# List windows
def "main windows" [] {
    msg windows | sort-by id
}

# Get the focused workspace
def "main focused-workspace" [] {
    main workspaces | where is_focused == true | first
}

# List workspaces
def "main workspaces" [] {
    msg workspaces | sort-by idx
}

