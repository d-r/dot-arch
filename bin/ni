#!/usr/bin/env nu

use ~/dot/nu/niri.nu *
use ~/dot/nu/kit.nu *

alias perform = niri msg action

# The knight who says "Ni!"
def main [] {
    help main
}

# Spawn a terminal
def "main toggle-term" [] {
    let $ws = (main focused-workspace)
    let $w = (nim windows | where workspace_id == $ws.id | where app_id == term | first?)
    if ($w | is-empty) {
        kitty --app-id term
    } else if $w.is_focused {
        perform focus-window-previous
    } else {
        perform focus-window --id $w.id
    }
}

# Run a CLI app inside of a terminal
def "main spawn-cli-app" [$id: string, ...$cmd] {
    kitty --app-id $id ...$cmd
}

# Pick something off a menu
def "main pick" [] {
    help main pick
}

# Pick a bookmark
def "main pick bookmark" [] {
    modal bm
}

# Pick an application
def "main pick app" [] {
    modal app
}

#-------------------------------------------------------------------------------

def "main focused-workspace" [] {
    nim workspaces | where is_focused == true | first
}

#-------------------------------------------------------------------------------

# Run a command inside of a modal terminal
def --wrapped modal [...$cmd] {
    let $pid = (nim windows | where app_id == modal | get pid.0?)
    if ($pid | is-empty) {
        kitty --config ~/.config/kitty/modal.conf --app-id modal ...$cmd
    } else {
        kill $pid
    }
}
