#!/usr/bin/env nu

use ~/dot/nu/kit.nu *
use ~/dot/nu/niri.nu *

alias spawn = wm spawn

# Uh...
def main [] {
    help main
}

# Pick something from a menu
def "main pick" [] {
    help main pick
}

# Pick a bookmark
def "main pick bookmark" [] {
    main modal visit bookmark
}

# Save the URL inside the clipboard
def "main save bookmark" [] {
    let $url = (wl-paste)
    if ($url | is-not-empty) {
        main modal save-url $url
    }
}

# Pick an application
def "main pick app" [] {
    main modal visit app
}

# Pick a project
def "main pick project" [] {
    main modal visit project
}

# Run a Nushell command inside of a modal terminal
def "main modal" [...$cmd: string] {
    let $pid = (wm windows | where app_id == modal | get pid.0?)
    if ($pid | is-empty) {
        let $cmd = $cmd | str join " "
        main term nu "-c" $cmd --config modal --app-id modal
    } else {
        kill $pid
    }
}

# Spawn or focus a terminal in the current workspace
def "main toggle-term" [] {
    let $w = (wm windows-in-focused-workspace | where app_id == term | first?)
    if ($w | is-empty) {
        main term --app-id term
    } else if $w.is_focused {
        wm focus-previous-window
    } else {
        wm focus-window $w.id
    }
}

# Spawn a terminal and optionally run a shell command inside of it
def "main term" [...$cmd: string, --config:string, --app-id:string] {
    let $config = ($config | default kitty) ++ ".conf"
    let $config = ("~/.config/kitty" | path expand | path join $config)
    let $app_id = ($app_id | default kitty)
    spawn kitty --config $config --app-id $app_id -d (main cwd) -- ...$cmd
}

# Get the working directory for the focused workspace
def "main cwd" [] {
    match (wm focused-workspace).name {
        "web" => "~/dl"
        "dot" => "~/dot"
        "dev" => "~/src"
        "vlt" => "~/vault"
        _ => "~"
    }
    | path expand
}
